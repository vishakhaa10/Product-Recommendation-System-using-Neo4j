{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Northwind Recommendation Engine\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    ":play northwind graph"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "pip install icypher"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%load_ext icypher"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%cypher http://user:passwd@localhost:7474/db/data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "CREATE INDEX ON :Product(productID);\n",
    "CREATE INDEX ON :Product(productName);\n",
    "CREATE INDEX ON :Category(categoryID);\n",
    "CREATE INDEX ON :Employee(employeeID);\n",
    "CREATE INDEX ON :Supplier(supplierID);\n",
    "CREATE INDEX ON :Customer(customerID);\n",
    "CREATE INDEX ON :Customer(customerName);\n",
    "CREATE CONSTRAINT ON (o:Order) ASSERT o.orderID IS UNIQUE;\n",
    "\n",
    "// Create customers\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/customers.csv\" AS row\n",
    "CREATE (:Customer {companyName: row.companyName, customerID: row.customerID, fax: row.fax, phone: row.phone});\n",
    "\n",
    "// Create products\n",
    "USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/products.csv\" AS row\n",
    "CREATE (:Product {productName: row.productName, productID: row.productID, unitPrice: toFloat(row.UnitPrice)});\n",
    "\n",
    "// Create suppliers\n",
    "USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/suppliers.csv\" AS row\n",
    "CREATE (:Supplier {companyName: row.companyName, supplierID: row.supplierID});\n",
    "\n",
    "// Create employees\n",
    "USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/employees.csv\" AS row\n",
    "CREATE (:Employee {employeeID:row.employeeID,  firstName: row.firstName, lastName: row.lastName, title: row.title});\n",
    "\n",
    "// Create categories\n",
    "USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/categories.csv\" AS row\n",
    "CREATE (:Category {categoryID: row.categoryID, categoryName: row.categoryName, description: row.description});\n",
    "\n",
    "// Create Orders\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/orders.csv\" AS row\n",
    "MERGE (o:Order {orderID: row.orderID}) ON CREATE SET o.shipName =  row.shipName;\n",
    "\n",
    "// Relate orders to products\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/order-details.csv\" AS row\n",
    "MATCH (o:Order {orderID: row.orderID})\n",
    "MATCH (product:Product {productID: row.productID})\n",
    "MERGE (o)-[pu:PRODUCT]->(product)\n",
    "ON CREATE SET pu.unitPrice = toFloat(row.unitPrice), pu.quantity = toFloat(row.quantity);\n",
    "\n",
    "// Relate Orders to Employees\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/orders.csv\" AS row\n",
    "MATCH (o:Order {orderID: row.orderID})\n",
    "MATCH (employee:Employee {employeeID: row.employeeID})\n",
    "MERGE (employee)-[:SOLD]->(o);\n",
    "\n",
    "// Relate customers to orders\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/orders.csv\" AS row\n",
    "MATCH (o:Order {orderID: row.orderID})\n",
    "MATCH (customer:Customer {customerID: row.customerID})\n",
    "MERGE (customer)-[:PURCHASED]->(o);\n",
    "\n",
    "// Relate Products to suppliers\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/products.csv\" AS row\n",
    "MATCH (product:Product {productID: row.productID})\n",
    "MATCH (supplier:Supplier {supplierID: row.supplierID})\n",
    "MERGE (supplier)-[:SUPPLIES]->(product);\n",
    "\n",
    "// Relate Products to Categories\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/products.csv\" AS row\n",
    "MATCH (product:Product {productID: row.productID})\n",
    "MATCH (category:Category {categoryID: row.categoryID})\n",
    "MERGE (product)-[:PART_OF]->(category);\n",
    "\n",
    "// Relate employees to managers\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/employees.csv\" AS row\n",
    "MATCH (employee:Employee {employeeID: row.employeeID})\n",
    "MATCH (manager:Employee {employeeID: row.reportsTo})\n",
    "MERGE (employee)-[:REPORTS_TO]->(manager);\n",
    "\n",
    "// Add unit price\n",
    " USING PERIODIC COMMIT 100\n",
    "LOAD CSV WITH HEADERS FROM \"https://raw.githubusercontent.com/adam-cowley/northwind-neo4j/master/data/order-details.csv\" AS row\n",
    "MATCH (o:Order {orderID: row.orderID})\n",
    "MATCH (product:Product {productID: row.productID})\n",
    "MERGE (o)-[pu:PRODUCT]->(product)\n",
    "ON CREATE SET pu.unitPrice = toFloat(row.unitPrice), pu.quantity = toFloat(row.quantity);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Dataset\n",
    "\n",
    "\n",
    "![7a4346ed 0bdb 4f6b baeb 231c6b0b0bd2 HZuG Jml9](https://ik.imagekit.io/ggp/7a4346ed-0bdb-4f6b-baeb-231c6b0b0bd2_HZuG-Jml9)\n",
    "\n",
    "The Northwind Graph provides us with a rich dataset, but primarily we&#8217;re interested in Customers and their Orders.   In a Graph, the data is modelled like so:\n",
    "\n",
    "![4c559e14 baaa 4d77 ad62 f3f52deab74a q6scT bsop](https://ik.imagekit.io/ggp/4c559e14-baaa-4d77-ad62-f3f52deab74a_q6scT_bsop)\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Popular Products\n",
    "\n",
    "To find the most popular products in the dataset, we can follow the path from `:Customer` to `:Product`\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "match (c:Customer)-[:PURCHASED]->(o:Order)-[:PRODUCT]->(p:Product)\n",
    "return c.companyName, p.productName, count(o) as orders\n",
    "order by orders desc\n",
    "limit 5"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Content Based Recommendations\n",
    "\n",
    "The simplest recommendation we can make for a Customer is a content based recommendation.\n",
    "Based on their previous purchases, can we recommend them anything that they haven&#8217;t already bought?\n",
    "For every product our customer has purchased, let&#8217;s see what other customers have also purchased.\n",
    "Each `:Product` is related to a `:Category`  so we can use this to further narrow down the list of products to recommend.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "match (c:Customer)-[:PURCHASED]->(o:Order)-[:PRODUCT]->(p:Product)\n",
    "<-[:PRODUCT]-(o2:Order)-[:PRODUCT]->(p2:Product)-[:PART_OF]->(:Category)<-[:PART_OF]-(p)\n",
    "WHERE c.customerID = 'ANTON' and NOT( (c)-[:PURCHASED]->(:Order)-[:PRODUCT]->(p2) )\n",
    "return c.companyName, p.productName as has_purchased, p2.productName as has_also_purchased, count(DISTINCT o2) as occurrences\n",
    "order by occurrences desc\n",
    "limit 5"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Collaborative Filtering\n",
    "\n",
    "Collaborative Filtering is a technique used by recommendation engines to recommend content based on the feedback from other Customers.\n",
    "To do this, we can use the k-NN (k-nearest neighbors) Algorithm.\n",
    "k-N works by grouping items into classifications based on their similarity to eachother.\n",
    "In our case, this could be ratings between two Customers for a Product.\n",
    "To give a real world example, this is how sites like Netflix make recommendations based on the ratings given to shows you&#8217;ve already watched.\n",
    "The first thing we need to do to make this model work is create some \"ratings relationships\".\n",
    "For now, let&#8217;s create a score somewhere between 0 and 1 for each product based on the number of times a customer has purchased a product.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH (c:Customer)-[:PURCHASED]->(o:Order)-[:PRODUCT]->(p:Product)\n",
    "WITH c, count(p) as total\n",
    "MATCH (c)-[:PURCHASED]->(o:Order)-[:PRODUCT]->(p:Product)\n",
    "WITH c, total,p, count(o)*1.0 as orders\n",
    "MERGE (c)-[rated:RATED]->(p)\n",
    "ON CREATE SET rated.rating = orders/total\n",
    "ON MATCH SET rated.rating = orders/total\n",
    "WITH c.companyName as company, p.productName as product, orders, total, rated.rating as rating\n",
    "ORDER BY rating DESC\n",
    "RETURN company, product, orders, total, rating LIMIT 10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now our model should look something like this:\n",
    "\n",
    "![33d26c02 b28c 4d82 9be4 ffb024550153 1NcVSJLAC](https://ik.imagekit.io/ggp/33d26c02-b28c-4d82-9be4-ffb024550153_1NcVSJLAC)\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH (me:Customer)-[r:RATED]->(p:Product)\n",
    "WHERE me.customerID = 'ANTON'\n",
    "RETURN p.productName, r.rating limit 10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now we can use these ratings to compare the preferences of two Customers.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "// See Customer's Similar Ratings to Others\n",
    "MATCH (c1:Customer {customerID:'ANTON'})-[r1:RATED]->(p:Product)<-[r2:RATED]-(c2:Customer)\n",
    "RETURN c1.customerID, c2.customerID, p.productName, r1.rating, r2.rating,\n",
    "CASE WHEN r1.rating-r2.rating < 0 THEN -(r1.rating-r2.rating) ELSE r1.rating-r2.rating END as difference\n",
    "ORDER BY difference ASC\n",
    "LIMIT 15"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now, we can create a similarity score between two Customers using Cosine Similarity (Hat tip to Nicole White for the original Cypher query&#8230;&#8203;)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH (c1:Customer)-[r1:RATED]->(p:Product)<-[r2:RATED]-(c2:Customer)\n",
    "WITH\n",
    "\tSUM(r1.rating*r2.rating) as dot_product,\n",
    "\tSQRT( REDUCE(x=0.0, a IN COLLECT(r1.rating) | x + a^2) ) as r1_length,\n",
    "\tSQRT( REDUCE(y=0.0, b IN COLLECT(r2.rating) | y + b^2) ) as r2_length,\n",
    "\tc1,c2\n",
    "MERGE (c1)-[s:SIMILARITY]-(c2)\n",
    "SET s.similarity = dot_product / (r1_length * r2_length)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH (me:Customer)-[r:SIMILARITY]->(them)\n",
    "WHERE me.customerID='ANTON'\n",
    "RETURN me.companyName, them.companyName, r.similarity\n",
    "ORDER BY r.similarity DESC limit 10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Great, let&#8217;s now make a recommendation based on these similarity scores.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "metadata": {
    "slideshow": {
     "slide_type": "fragment"
    }
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "WITH 1 as neighbours\n",
    "MATCH (me:Customer)-[:SIMILARITY]->(c:Customer)-[r:RATED]->(p:Product)\n",
    "WHERE me.customerID = 'ANTON' and NOT ( (me)-[:RATED|PRODUCT|ORDER*1..2]->(p:Product) )\n",
    "WITH p, COLLECT(r.rating)[0..neighbours] as ratings, collect(c.companyName)[0..neighbours] as customers\n",
    "WITH p, customers, REDUCE(s=0,i in ratings | s+i) / LENGTH(ratings)  as recommendation\n",
    "ORDER BY recommendation DESC\n",
    "RETURN p.productName, customers, recommendation LIMIT 10"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
